import Bottcher.Dyadic
import Bottcher.NormSq
import Bottcher.Pray
import Bottcher.Rat
import Ray.Dynamics.Mandelbrot
import Ray.Dynamics.Multibrot.Area

/-!
# Upper bounds on the area of the Mandelbrot set
-/

open MeasureTheory (volume)
open scoped Real

variable {n : â„•}
local instance : Fact (2 â‰¤ 2) := âŸ¨by norm_numâŸ©
variable {Î± : Type} [SeriesScalar Î±] [ApproxSeries Î± â„‚] [Div2 Î±] [ApproxDiv2 Î± â„‚] [NormSq Î±]
variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]

/-!
### Mandelbrot area upper bound, in rational form
-/

/-- Our area upper bound divided by `Ï€` so that it is rational -/
noncomputable def upper (n : â„•) : â„ :=
  âˆ‘ k âˆˆ Finset.range n, (1 - k) * â€–series_coeff k (pray 2) 0â€– ^ 2

/-- `Ï€ * upper n` is a Mandelbrot area upper bound -/
lemma area_mandelbrot_le_upper (n0 : n â‰  0) : volume.real mandelbrot â‰¤ Ï€ * upper n := by
  rw [mandelbrot_eq_multibrot]
  induction' n with n h
  Â· simp at n0
  Â· refine le_trans (multibrot_volume_le (Finset.range n)) (le_of_eq ?_)
    simp only [upper, Finset.sum_range_succ', series_coeff_zero', pray_zero, Nat.cast_zero,
      sub_zero, norm_one, one_pow, mul_add, mul_one, Nat.cast_add_one,
      sub_add_cancel_right, neg_mul, Finset.sum_neg_distrib, mul_neg, Finset.mul_sum, â† mul_assoc]
    ring_nf
    simp only [series_coeff, smul_eq_mul, mul_comm]

/-!
### Series approximation of `upper`
-/

/-- Series approximation of `upper` -/
def supper (n : â„•) : Î± :=
  ((spray n).c.mapIdx fun k (x : Î±) â†¦ (1 - k) * NormSq.normSq x).sum
